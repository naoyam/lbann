enable_language(C)
enable_language(CXX)
enable_language(Fortran)# FIXME: Really??

# Add all the OpenCV options; this is just for the GUI, of course
list(APPEND CMAKE_MODULE_PATH "${CMAKE_CURRENT_SOURCE_DIR}")
include(AddOpenCVOptions)

set(OPENCV_CMAKE_INSTALL_PREFIX "${CMAKE_INSTALL_PREFIX}"
  CACHE PATH "The installation destination of OpenCV")

set(OPENCV_CMAKE_BUILD_TYPE "${CMAKE_BUILD_TYPE}"
  CACHE STRING "The build type for OpenCV.")

if (TARGET JPEG-TURBO)
  set(_opencv_depends_tag DEPENDS)
  list(APPEND _OPENCV_DEPENDS JPEG-TURBO)
  set(OPENCV_WITH_LIBJPEG_TURBO ON)
  set(OPENCV_BUILD_JPEG OFF)
  set(OPENCV_CMAKE_PREFIX_PATH ${CMAKE_PREFIX_PATH} "${JPEG-TURBO_CMAKE_INSTALL_PREFIX}")
endif ()

# Get the list of opencv variables
get_property(OPENCV_VARIABLES DIRECTORY PROPERTY VARIABLES)
list(FILTER OPENCV_VARIABLES INCLUDE REGEX "^OPENCV_.*")

create_cmake_arguments(
  OUTPUT_VARIABLE OPENCV_CMAKE_ARGS
  PACKAGE_NAME OPENCV
  REMOVE_PKG_NAME
  VARIABLES ${OPENCV_VARIABLES})

option(OPENCV_CLONE_VIA_SSH
  "Clone OPENCV using SSH instead of HTTPS" ${LBANN_SB_CLONE_VIA_SSH})

if (OPENCV_CLONE_VIA_SSH)
  set(OPENCV_URL git@github.com:opencv/opencv.git
    CACHE STRING "The URL from which to clone OPENCV")
else ()
  set(OPENCV_URL "https://github.com/opencv/opencv"
    CACHE STRING "The URL from which to clone OpenCV")
endif ()

set(OPENCV_TAG "4.1.0"
  CACHE STRING "The git tag or hash to checkout for OpenCV")

if (OPENCV_CUSTOM_SOURCE_DIR)
  set(OPENCV_SOURCE_DIR "${OPENCV_CUSTOM_SOURCE_DIR}")
  set(OPENCV_URL "")
  set(OPENCV_TAG "")
  set(_GIT_REPOSITORY_TAG)
  set(_GIT_TAG_TAG)
  message(STATUS "Using OPENCV source in: ${OPENCV_SOURCE_DIR}")
else ()
  set(OPENCV_SOURCE_DIR "${CMAKE_CURRENT_BINARY_DIR}/src")
  set(_GIT_REPOSITORY_TAG "GIT_REPOSITORY")
  set(_GIT_TAG_TAG "GIT_TAG")
endif ()

include(ExternalProject)
ExternalProject_Add(OPENCV
  PREFIX ${CMAKE_CURRENT_BINARY_DIR}
  TMP_DIR ${CMAKE_CURRENT_BINARY_DIR}/tmp
  STAMP_DIR ${CMAKE_CURRENT_BINARY_DIR}/stamp
  ${_GIT_REPOSITORY_TAG} ${OPENCV_URL}
  ${_GIT_TAG_TAG} ${OPENCV_TAG}
  SOURCE_DIR ${OPENCV_SOURCE_DIR}
  ${_opencv_depends_tag} ${_OPENCV_DEPENDS}
  BINARY_DIR ${CMAKE_CURRENT_BINARY_DIR}/build
  INSTALL_DIR ${OPENCV_CMAKE_INSTALL_PREFIX}
  USES_TERMINAL_BUILD 1
  LOG_DOWNLOAD 1
  LOG_UPDATE 1
  LOG_CONFIGURE 1
  LOG_BUILD 1
  LOG_INSTALL 1
  LOG_TEST 1
  LIST_SEPARATOR |
  CMAKE_ARGS
  -DCMAKE_C_COMPILER=${CMAKE_C_COMPILER}
  -DCMAKE_CXX_COMPILER=${CMAKE_CXX_COMPILER}
  -DCMAKE_Fortran_COMPILER=${CMAKE_Fortran_COMPILER}
  -DCMAKE_C_FLAGS=${CMAKE_C_FLAGS}
  -DCMAKE_CXX_FLAGS=${CMAKE_CXX_FLAGS}
  -DCMAKE_Fortran_FLAGS=${CMAKE_Fortran_FLAGS}
  -DCMAKE_BUILD_TYPE=${OPENCV_CMAKE_BUILD_TYPE}
  ${OPENCV_CMAKE_ARGS}
  )

set(OPENCV_DIR ${OPENCV_CMAKE_INSTALL_PREFIX}
  CACHE INTERNAL "The install prefix of OPENCV.")
