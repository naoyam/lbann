enable_language(C)
enable_language(CXX)
enable_language(ASM_NASM)

# Match the jpeg-turbo default
option(JPEG-TURBO_ENABLE_STATIC "Enable the jpeg-turbo static linkage." ON)

option(JPEG-TURBO_CLONE_VIA_SSH
  "Clone JPEG-TURBO using SSH instead of HTTPS" ${LBANN_SB_CLONE_VIA_SSH})

if (JPEG-TURBO_CLONE_VIA_SSH)
  set(JPEG-TURBO_URL git@github.com:libjpeg-turbo/libjpeg-turbo
    CACHE STRING "The URL from which to clone JPEG-TURBO")
else ()
  set(JPEG-TURBO_URL https://github.com/libjpeg-turbo/libjpeg-turbo
    CACHE STRING "The URL from which to clone LIBJPEG-TURBO")
endif ()

set(JPEG-TURBO_TAG "2.0.3"
  CACHE STRING "The git tag to checkout for LIBJPEG-TURBO")

# Where to install LIBJPEG-TURBO
set (JPEG-TURBO_CMAKE_INSTALL_PREFIX "${CMAKE_INSTALL_PREFIX}"
  CACHE PATH "The installation location of LIBJPEG-TURBO.")

if (JPEG_TURBO_CUSTOM_SOURCE_DIR)
  set(JPEG_TURBO_SOURCE_DIR "${JPEG_TURBO_CUSTOM_SOURCE_DIR}")
  set(JPEG-TURBO_URL "")
  set(JPEG-TURBO_TAG "")
  set(_GIT_REPOSITORY_TAG)
  set(_GIT_TAG_TAG)
  message(STATUS "Using JPEG-TURBO source in: ${JPEG_TURBO_SOURCE_DIR}")
else ()
  set(JPEG_TURBO_SOURCE_DIR "${CMAKE_CURRENT_BINARY_DIR}/src")
  set(_GIT_REPOSITORY_TAG "GIT_REPOSITORY")
  set(_GIT_TAG_TAG "GIT_TAG")
endif ()

# Handle the install of LIBJPEG-TURBO
include(ExternalProject)
ExternalProject_Add(JPEG-TURBO
  PREFIX ${CMAKE_CURRENT_BINARY_DIR}
  TMP_DIR ${CMAKE_CURRENT_BINARY_DIR}/tmp
  STAMP_DIR ${CMAKE_CURRENT_BINARY_DIR}/stamp
  ${_GIT_REPOSITORY_TAG} ${JPEG-TURBO_URL}
  ${_GIT_TAG_TAG} ${JPEG-TURBO_TAG}
  SOURCE_DIR ${JPEG_TURBO_SOURCE_DIR}
  BINARY_DIR ${CMAKE_CURRENT_BINARY_DIR}/build
  INSTALL_DIR ${JPEG-TURBO_CMAKE_INSTALL_PREFIX}
  USES_TERMINAL_BUILD 1
  LOG_DOWNLOAD 1
  LOG_UPDATE 1
  LOG_CONFIGURE 1
  LOG_BUILD 1
  LOG_INSTALL 1
  LOG_TEST 1
  CMAKE_ARGS
  -G${CMAKE_GENERATOR}
  -DCMAKE_INSTALL_PREFIX=${JPEG-TURBO_CMAKE_INSTALL_PREFIX}
  -DCMAKE_BUILD_TYPE=${JPEG-TURBO_CMAKE_BUILD_TYPE}
  -DCMAKE_C_COMPILER=${CMAKE_C_COMPILER}
  -DCMAKE_CXX_COMPILER=${CMAKE_CXX_COMPILER}
  -DENABLE_STATIC=${JPEG-TURBO_ENABLE_STATIC}
  -DCMAKE_MACOSX_RPATH=ON
  )

  set(JPEG-TURBO_DIR ${JPEG-TURBO_CMAKE_INSTALL_PREFIX}
    CACHE INTERNAL "The install prefix of JPEG-TURBO.")
