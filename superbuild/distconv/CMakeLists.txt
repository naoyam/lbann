enable_language(CXX)

set(DISTCONV_URL ssh://git@cz-bitbucket.llnl.gov:7999/~maruyama/distconv.git
  CACHE STRING "The URL from which to clone Distconv")

set(DISTCONV_TAG "master"
  CACHE STRING "The git tag to checkout for Distconv")

set(DISTCONV_CMAKE_BUILD_TYPE "${CMAKE_BUILD_TYPE}"
  CACHE STRING "The build type for Distconv.")

# Where to install Distconv
set(DISTCONV_CMAKE_INSTALL_PREFIX "${CMAKE_INSTALL_PREFIX}"
  CACHE PATH "The installation location of Distconv.")

# Aluminum
set(LBANN_SB_FWD_DISTCONV_Aluminum_DIR
  "${ALUMINUM_DIR}/lib64/cmake/aluminum"
  CACHE STRING "The path to Aluminum for Distconv.")
list(APPEND _DISTCONV_DEPENDS Aluminum)

# P2P
set(LBANN_SB_FWD_DISTCONV_P2P_DIR
  "${P2P_DIR}"
  CACHE STRING "The path to P2P for Distconv.")
list(APPEND _DISTCONV_DEPENDS P2P)

# Get the list of Distconv variables
get_property(DISTCONV_VARIABLES DIRECTORY PROPERTY VARIABLES)
list(FILTER DISTCONV_VARIABLES INCLUDE REGEX
  "^DISTCONV_.*\|^LBANN_SB_FWD_DISTCONV_.*")
list(FILTER DISTCONV_VARIABLES EXCLUDE REGEX "DISTCONV_URL\|DISTCONV_TAG")

create_cmake_arguments(
  OUTPUT_VARIABLE DISTCONV_CMAKE_ARGS
  PACKAGE_NAME DISTCONV
  SKIP_VARS_WITH_PREFIXES "LBANN_SB"
  EXTRA_REMOVE_PREFIXES "LBANN_SB_FWD_DISTCONV"
  VARIABLES ${DISTCONV_VARIABLES})

message(STATUS ${DISTCONV_VARIABLES})
message(STATUS ${DISTCONV_CMAKE_ARGS})

include(ExternalProject)
ExternalProject_Add(DISTCONV
  PREFIX ${CMAKE_CURRENT_BINARY_DIR}
  TMP_DIR ${CMAKE_CURRENT_BINARY_DIR}/tmp
  STAMP_DIR ${CMAKE_CURRENT_BINARY_DIR}/stamp
  GIT_REPOSITORY ${DISTCONV_URL}
  GIT_TAG ${DISTCONV_TAG}
  DEPENDS ${_DISTCONV_DEPENDS}
  SOURCE_DIR ${CMAKE_CURRENT_BINARY_DIR}/src
  BINARY_DIR ${CMAKE_CURRENT_BINARY_DIR}/build
  INSTALL_DIR ${DISTCONV_CMAKE_INSTALL_PREFIX}
  USES_TERMINAL_BUILD 1
  LOG_DOWNLOAD 1
  LOG_UPDATE 1
  LOG_CONFIGURE 1
  LOG_BUILD 1
  LOG_INSTALL 1
  LOG_TEST 1
  LIST_SEPARATOR |
  CMAKE_ARGS
  -DCMAKE_CXX_COMPILER=${CMAKE_CXX_COMPILER}
  -DCMAKE_CXX_FLAGS=${CMAKE_CXX_FLAGS}
  -DCMAKE_CUDA_COMPILER=${CMAKE_CUDA_COMPILER}
  -DCMAKE_CUDA_FLAGS=${CMAKE_CUDA_FLAGS}
  -DCMAKE_CUDA_HOST_COMPILER=${CMAKE_CUDA_HOST_COMPILER}
  ${DISTCONV_CMAKE_ARGS}
  )

set(DISTCONV_DIR ${DISTCONV_CMAKE_INSTALL_PREFIX}
  CACHE INTERNAL "The install prefix of Distconv.")
